apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.adarshr.test-logger'
apply from: 'gradleScript/dependencies.gradle'

def config = rootProject.ext

// Setup EWallet client data
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def appVersion = properties.getProperty("app.version")
def clientAPIBaseURL = properties.getProperty('ewallet.baseurl')
def clientAPISocketBaseURL = properties.getProperty('ewallet.socket.baseurl')
def clientEncryptKeyAlias = properties.getProperty('omg.encrypt.keyAlias')
def clientEncryptIV = properties.getProperty('omg.encrypt.iv')

android {
    buildToolsVersion config.buildToolsVersion
    compileSdkVersion config.compileSdkVersion
    defaultConfig {
        applicationId config.applicationId
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode config.versionCode
        versionName appVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            ext.enableCrashlytics = monitorCrash
            buildConfigField 'String', "CLIENT_API_BASE_URL", clientAPIBaseURL
            buildConfigField 'String', "CLIENT_API_SOCKET_BASE_URL", clientAPISocketBaseURL
            buildConfigField 'String', "CLIENT_ENCRYPT_KEY_ALIAS", clientEncryptKeyAlias
            buildConfigField 'String', "CLIENT_ENCRYPT_IV", clientEncryptIV
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', "CLIENT_API_BASE_URL", clientAPIBaseURL
            buildConfigField 'String', "CLIENT_API_SOCKET_BASE_URL", clientAPISocketBaseURL
            buildConfigField 'String', "CLIENT_ENCRYPT_KEY_ALIAS", clientEncryptKeyAlias
            buildConfigField 'String', "CLIENT_ENCRYPT_IV", clientEncryptIV
        }
    }
    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        disable 'ParcelCreator'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

testlogger {
    showSummary true
    theme 'mocha'
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "pos-merchant-${variant.name}-${variant.versionName}.apk"
    }
}

androidExtensions {
    experimental = true
}

repositories {
    mavenCentral()
}

dependencies {
    /* OmiseGO */
    implementation omisego.core
    implementation omisego.admin

    /* Kotlin */
    implementation kotlinDependencies.stdlib
    implementation kotlinDependencies.coroutines

    /* Navigation Component */
    implementation navigation.navFragment
    implementation navigation.navUI
    /* Architecture Component */
    implementation arch.viewmodel
    implementation arch.extensions
    /* Databinding */
    kapt databinding.compiler
    /* Support library */
    implementation supportDependencies.appCompat
    implementation supportDependencies.design
    implementation supportDependencies.cardView
    /* Networking */
    implementation retrofitDependencies.retrofit
    implementation retrofitDependencies.converterGson
    /* Load image */
    implementation glide.glide

    implementation custom.constraintLayout
    implementation custom.shimmer
    implementation custom.stetho
    implementation custom.dropdown
    implementation custom.dexter
    implementation custom.biometric
    implementation okHttp3.loggingIntercepter
    implementation firebase.crashlytics

    testImplementation test
}

apply plugin: 'com.google.gms.google-services'
